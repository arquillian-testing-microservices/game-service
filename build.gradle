buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.3'
    }
}

plugins {
    id "io.spring.dependency-management" version "0.6.1.RELEASE"
}

apply plugin: 'war'

allprojects {
    group = 'org.arquillian.microservices'
    version = '1.0.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

dependencies {
    providedCompile 'javax:javaee-api:7.0'
    compile 'org.apache.deltaspike.core:deltaspike-core-api'
    runtime 'org.apache.deltaspike.core:deltaspike-core-impl'
    // tag::contract-tests[]
    testCompile 'org.jboss.arquillian.junit:arquillian-junit-container'
    testCompile 'junit:junit:4.12'
    testCompile 'org.arquillian.container:arquillian-container-chameleon:1.0.0.Alpha7'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-api-maven'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven-archive'
    testCompile 'org.arquillian.pact:arquillian-pact-consumer-core:1.0.0.Final-SNAPSHOT'
    testCompile 'org.arquillian.pact:arquillian-pact-consumer-git-publisher:1.0.0.Final-SNAPSHOT'
    testCompile 'au.com.dius:pact-jvm-consumer_2.11:3.5.0-beta.2'
    // end::contract-tests[]
}

dependencyManagement {
    imports {
        mavenBom 'org.apache.deltaspike.distribution:distributions-bom:1.7.1'
        // tag::contract-tests[]
        mavenBom 'org.jboss.arquillian:arquillian-bom:1.1.11.Final'
        mavenBom 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-depchain:2.2.4'
        // end::contract-tests[]
    }
}

test {
    systemProperties "artifactversion", version
}


war {
    archiveName 'game-service.war'
}

apply plugin: 'com.bmuschko.docker-remote-api'
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

docker {
    if (System.env.containsKey('DOCKER_HOST') && System.env.containsKey('DOCKER_CERT_PATH')) {
        url = System.env.DOCKER_HOST.replace("tcp", "https")
        certPath = new File(System.env.DOCKER_CERT_PATH)
    }
}

task buildImage(type: DockerBuildImage) {
    dependsOn assemble
    inputDir = project.rootDir
    tag = "arquillian/game-service:${project.version}"
}